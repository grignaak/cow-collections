group 'com.github.grignaak'
version '1.0.0'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

apply plugin: 'java'

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.findbugs:jsr305:3.0.2"

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def sonatypeUser = project.hasProperty('sonatypeUsername') ? sonatypeUsername : null
def sonatypePass = project.hasProperty('sonatypePassword') ? sonatypePassword : null

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            pom.project {
                name 'cow collections'
                packaging 'jar'
                description 'Copy-on-write collections for easy, thread-safe, immutability'
                url 'http://github.com/grignaak/cow-collections'

                scm {
                    url 'scm:git@github.com:grignaak/cow-collections.git'
                    connection 'scm:git@github.com:grignaak/cow-collections.git'
                    developerConnection 'scm:git@github.com:grignaak/cow-collections.git'
                }

                licenses {
                    license {
                        name 'The MIT Licence'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'grignaak'
                        name 'Michael Deardeuff'
                    }
                }
            }
        }
    }
}
